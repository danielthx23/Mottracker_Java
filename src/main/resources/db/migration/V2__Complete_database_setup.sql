-- ============================================================
-- Migration V2: Setup Completo do Banco de Dados
-- Consolida todas as migrations em uma única
-- ============================================================

-- ============================================================
-- PARTE 1: Criação de Tabelas
-- ============================================================

CREATE TABLE mt_usuario_java (
    id_usuario NUMBER(19,0) GENERATED BY DEFAULT AS IDENTITY,
    nome_usuario VARCHAR2(255 CHAR) NOT NULL,
    cpf_usuario VARCHAR2(255 CHAR) NOT NULL,
    email_usuario VARCHAR2(255 CHAR) NOT NULL,
    senha_usuario VARCHAR2(255 CHAR) NOT NULL,
    cnh_usuario VARCHAR2(255 CHAR) NOT NULL,
    data_nascimento_usuario TIMESTAMP(6),
    criado_em_usuario TIMESTAMP(6),
    token_usuario VARCHAR2(255 CHAR),
    PRIMARY KEY (id_usuario)
);

CREATE TABLE mt_permissao_java (
    id_permissao NUMBER(19,0) GENERATED BY DEFAULT AS IDENTITY,
    nome_permissao VARCHAR2(100 CHAR) NOT NULL,
    descricao VARCHAR2(255 CHAR),
    PRIMARY KEY (id_permissao)
);

CREATE TABLE mt_usuario_permissao_java (
    usuario_id NUMBER(19,0) NOT NULL,
    permissao_id NUMBER(19,0) NOT NULL,
    papel VARCHAR2(255 CHAR),
    PRIMARY KEY (usuario_id, permissao_id),
    FOREIGN KEY (usuario_id) REFERENCES mt_usuario_java(id_usuario),
    FOREIGN KEY (permissao_id) REFERENCES mt_permissao_java(id_permissao)
);

CREATE TABLE mt_telefone_java (
    id_telefone NUMBER(19,0) GENERATED BY DEFAULT AS IDENTITY,
    numero VARCHAR2(255 CHAR) NOT NULL,
    tipo VARCHAR2(255 CHAR),
    usuario_id NUMBER(19,0),
    PRIMARY KEY (id_telefone),
    FOREIGN KEY (usuario_id) REFERENCES mt_usuario_java(id_usuario)
);

CREATE TABLE mt_patio_java (
    id_patio NUMBER(19,0) GENERATED BY DEFAULT AS IDENTITY,
    nome_patio VARCHAR2(255 CHAR) NOT NULL,
    motos_totais_patio NUMBER(10,0) NOT NULL,
    motos_disponiveis_patio NUMBER(10,0) NOT NULL,
    data_patio TIMESTAMP(6),
    PRIMARY KEY (id_patio)
);

CREATE TABLE mt_moto_java (
    id_moto NUMBER(19,0) GENERATED BY DEFAULT AS IDENTITY,
    placa_moto VARCHAR2(255 CHAR) NOT NULL,
    modelo_moto VARCHAR2(255 CHAR) NOT NULL,
    ano_moto NUMBER(10,0) NOT NULL,
    quilometragem_moto NUMBER(10,0) NOT NULL,
    identificador_moto VARCHAR2(255 CHAR),
    estado_moto NUMBER(3,0) CHECK (estado_moto BETWEEN 0 AND 3),
    condicoes_moto VARCHAR2(255 CHAR),
    moto_patio_atual_id NUMBER(19,0),
    moto_patio_origem_id NUMBER(19,0),
    contrato_moto_id NUMBER(19,0) UNIQUE,
    PRIMARY KEY (id_moto),
    FOREIGN KEY (moto_patio_atual_id) REFERENCES mt_patio_java(id_patio),
    FOREIGN KEY (moto_patio_origem_id) REFERENCES mt_patio_java(id_patio)
);

CREATE TABLE mt_contrato_java (
    id_contrato NUMBER(19,0) GENERATED BY DEFAULT AS IDENTITY,
    clausulas_contrato VARCHAR2(255 CHAR),
    data_de_entrada_contrato TIMESTAMP(6) NOT NULL,
    horario_de_devolucao_contrato TIMESTAMP(6) NOT NULL,
    data_de_expiracao_contrato TIMESTAMP(6) NOT NULL,
    data_ultima_renovacao_contrato TIMESTAMP(6),
    numero_renovacoes_contrato NUMBER(10,0) NOT NULL,
    renovacao_automatica_contrato NUMBER(1),
    valor_toral_contrato NUMBER(38,2) NOT NULL,
    quantidade_parcelas NUMBER(10,0) NOT NULL,
    ativo_contrato NUMBER(1),
    usuario_contrato_id NUMBER(19,0),
    moto_contrato_id NUMBER(19,0),
    PRIMARY KEY (id_contrato),
    FOREIGN KEY (usuario_contrato_id) REFERENCES mt_usuario_java(id_usuario),
    FOREIGN KEY (moto_contrato_id) REFERENCES mt_moto_java(id_moto),
    CONSTRAINT unique_usuario_moto_data UNIQUE (usuario_contrato_id, moto_contrato_id, data_de_entrada_contrato)
);

CREATE TABLE mt_endereco_java (
    id_endereco NUMBER(19,0) GENERATED BY DEFAULT AS IDENTITY,
    logradouro VARCHAR2(150 CHAR) NOT NULL,
    numero VARCHAR2(20 CHAR) NOT NULL,
    complemento VARCHAR2(100 CHAR),
    bairro VARCHAR2(100 CHAR) NOT NULL,
    cidade VARCHAR2(100 CHAR) NOT NULL,
    estado VARCHAR2(2 CHAR) NOT NULL,
    cep VARCHAR2(10 CHAR) NOT NULL,
    referencia VARCHAR2(100 CHAR),
    patio_endereco_id NUMBER(19,0) UNIQUE,
    PRIMARY KEY (id_endereco),
    FOREIGN KEY (patio_endereco_id) REFERENCES mt_patio_java(id_patio)
);

CREATE TABLE mt_layout_patio_java (
    id_layout_patio NUMBER(19,0) GENERATED BY DEFAULT AS IDENTITY,
    descricao VARCHAR2(255 CHAR),
    comprimento NUMBER(38,2) NOT NULL,
    largura NUMBER(38,2) NOT NULL,
    altura NUMBER(38,2),
    data_criacao TIMESTAMP(6),
    patio_layout_patio_id NUMBER(19,0) UNIQUE,
    PRIMARY KEY (id_layout_patio),
    FOREIGN KEY (patio_layout_patio_id) REFERENCES mt_patio_java(id_patio)
);

CREATE TABLE mt_camera_java (
    id_camera NUMBER(19,0) GENERATED BY DEFAULT AS IDENTITY,
    nome_camera VARCHAR2(255 CHAR) NOT NULL,
    ip_camera VARCHAR2(255 CHAR),
    posx FLOAT(24),
    posy FLOAT(24),
    status NUMBER(3,0) NOT NULL CHECK (status BETWEEN 0 AND 3),
    patio_id NUMBER(19,0),
    PRIMARY KEY (id_camera),
    FOREIGN KEY (patio_id) REFERENCES mt_patio_java(id_patio)
);

CREATE TABLE mt_qrcode_ponto_java (
    id_qr_code_ponto NUMBER(19,0) GENERATED BY DEFAULT AS IDENTITY,
    identificador_qr_code VARCHAR2(255 CHAR) NOT NULL,
    posx FLOAT(24) NOT NULL,
    posy FLOAT(24) NOT NULL,
    layout_patio_id NUMBER(19,0),
    PRIMARY KEY (id_qr_code_ponto),
    FOREIGN KEY (layout_patio_id) REFERENCES mt_layout_patio_java(id_layout_patio)
);

CREATE TABLE mt_auditoria (
  id_audit       INTEGER GENERATED BY DEFAULT ON NULL AS IDENTITY PRIMARY KEY,
  tabela_nome    VARCHAR2(100),
  usuario_nome   VARCHAR2(100),
  tipo_operacao  VARCHAR2(10),
  data_hora      DATE,
  valores_old    CLOB,
  valores_new    CLOB
);

-- ============================================================
-- PARTE 2: Comentários nas Tabelas
-- ============================================================

COMMENT ON TABLE mt_usuario_java IS 'Tabela de usuários do sistema';
COMMENT ON TABLE mt_permissao_java IS 'Tabela de permissões do sistema';
COMMENT ON TABLE mt_usuario_permissao_java IS 'Tabela de relacionamento usuário-permissão';
COMMENT ON TABLE mt_telefone_java IS 'Tabela de telefones dos usuários';
COMMENT ON TABLE mt_patio_java IS 'Tabela de pátios de estacionamento';
COMMENT ON TABLE mt_moto_java IS 'Tabela de motocicletas';
COMMENT ON TABLE mt_contrato_java IS 'Tabela de contratos de locação';
COMMENT ON TABLE mt_endereco_java IS 'Tabela de endereços dos pátios';
COMMENT ON TABLE mt_layout_patio_java IS 'Tabela de layouts dos pátios';
COMMENT ON TABLE mt_camera_java IS 'Tabela de câmeras de monitoramento';
COMMENT ON TABLE mt_qrcode_ponto_java IS 'Tabela de pontos QR Code nos pátios';
COMMENT ON TABLE mt_auditoria IS 'Tabela de auditoria de alterações';

-- ============================================================
-- PARTE 3: Inserção de Permissões Iniciais
-- ============================================================

MERGE INTO mt_permissao_java p
USING (
    SELECT 'ADMIN' AS nome_permissao, 'Administrador do sistema' AS descricao FROM dual
    UNION ALL
    SELECT 'GERENTE', 'Gerente de operações' FROM dual
    UNION ALL
    SELECT 'OPERADOR', 'Operador de pátio' FROM dual
    UNION ALL
    SELECT 'USER', 'Usuário comum' FROM dual
) s
ON (p.nome_permissao = s.nome_permissao)
WHEN NOT MATCHED THEN
    INSERT (nome_permissao, descricao)
    VALUES (s.nome_permissao, s.descricao);

-- ============================================================
-- PARTE 4: Inserção de Dados Iniciais
-- ============================================================

-- Insert admin user
INSERT INTO mt_usuario_java (nome_usuario, cpf_usuario, email_usuario, senha_usuario, cnh_usuario, data_nascimento_usuario, criado_em_usuario)
VALUES ('Administrador', '12345678901', 'admin@mottu.com', '$2a$10$N.zmdr9k7uOCQb376NoUnuTJ8iAt6Z5EHsM8lE9lBOsl7iwK7pJ5m', '123456789', SYSDATE, SYSDATE);

-- Insert manager user
INSERT INTO mt_usuario_java (nome_usuario, cpf_usuario, email_usuario, senha_usuario, cnh_usuario, data_nascimento_usuario, criado_em_usuario)
VALUES ('Gerente', '98765432109', 'gerente@mottu.com', '$2a$10$N.zmdr9k7uOCQb376NoUnuTJ8iAt6Z5EHsM8lE9lBOsl7iwK7pJ5m', '987654321', SYSDATE, SYSDATE);

-- Insert operator user
INSERT INTO mt_usuario_java (nome_usuario, cpf_usuario, email_usuario, senha_usuario, cnh_usuario, data_nascimento_usuario, criado_em_usuario)
VALUES ('Operador', '11122233344', 'operador@mottu.com', '$2a$10$N.zmdr9k7uOCQb376NoUnuTJ8iAt6Z5EHsM8lE9lBOsl7iwK7pJ5m', '111222333', SYSDATE, SYSDATE);

-- Insert regular user
INSERT INTO mt_usuario_java (nome_usuario, cpf_usuario, email_usuario, senha_usuario, cnh_usuario, data_nascimento_usuario, criado_em_usuario)
VALUES ('Usuário', '55566677788', 'usuario@mottu.com', '$2a$10$N.zmdr9k7uOCQb376NoUnuTJ8iAt6Z5EHsM8lE9lBOsl7iwK7pJ5m', '555666777', SYSDATE, SYSDATE);

-- Assign permissions
INSERT INTO mt_usuario_permissao_java (usuario_id, permissao_id, papel)
SELECT u.id_usuario, p.id_permissao, 'ADMIN'
FROM mt_usuario_java u, mt_permissao_java p
WHERE u.email_usuario = 'admin@mottu.com' AND p.nome_permissao = 'ADMIN';

INSERT INTO mt_usuario_permissao_java (usuario_id, permissao_id, papel)
SELECT u.id_usuario, p.id_permissao, 'GERENTE'
FROM mt_usuario_java u, mt_permissao_java p
WHERE u.email_usuario = 'gerente@mottu.com' AND p.nome_permissao = 'GERENTE';

INSERT INTO mt_usuario_permissao_java (usuario_id, permissao_id, papel)
SELECT u.id_usuario, p.id_permissao, 'OPERADOR'
FROM mt_usuario_java u, mt_permissao_java p
WHERE u.email_usuario = 'operador@mottu.com' AND p.nome_permissao = 'OPERADOR';

INSERT INTO mt_usuario_permissao_java (usuario_id, permissao_id, papel)
SELECT u.id_usuario, p.id_permissao, 'USER'
FROM mt_usuario_java u, mt_permissao_java p
WHERE u.email_usuario = 'usuario@mottu.com' AND p.nome_permissao = 'USER';

-- Insert sample patios
INSERT INTO mt_patio_java (nome_patio, motos_totais_patio, motos_disponiveis_patio, data_patio)
VALUES ('Pátio Central', 50, 45, SYSDATE);

INSERT INTO mt_patio_java (nome_patio, motos_totais_patio, motos_disponiveis_patio, data_patio)
VALUES ('Pátio Norte', 30, 28, SYSDATE);

-- Insert sample motorcycles
-- Estado: 0=RETIRADA, 1=NO_PATIO, 2=NO_PATIO_ERRADO, 3=NAO_DEVOLVIDA
INSERT INTO mt_moto_java (placa_moto, modelo_moto, ano_moto, quilometragem_moto, identificador_moto, estado_moto, condicoes_moto, moto_patio_atual_id)
VALUES ('ABC1234', 'Honda CG 160', 2023, 15000, 'MOTO001', 1, 'Excelente', 1);

INSERT INTO mt_moto_java (placa_moto, modelo_moto, ano_moto, quilometragem_moto, identificador_moto, estado_moto, condicoes_moto, moto_patio_atual_id)
VALUES ('DEF5678', 'Yamaha Fazer 250', 2022, 25000, 'MOTO002', 0, 'Boa', 1);

INSERT INTO mt_moto_java (placa_moto, modelo_moto, ano_moto, quilometragem_moto, identificador_moto, estado_moto, condicoes_moto, moto_patio_atual_id)
VALUES ('GHI9012', 'Kawasaki Ninja 300', 2023, 8000, 'MOTO003', 1, 'Excelente', 2);

-- ============================================================
-- PARTE 5: Funções Oracle
-- ============================================================

CREATE OR REPLACE FUNCTION criaJson (p_query IN VARCHAR2) RETURN CLOB IS
  v_cursor     INTEGER;
  v_colcnt     INTEGER;
  v_desc_tab   DBMS_SQL.DESC_TAB;
  v_status     INTEGER;
  v_varchar2   VARCHAR2(4000);
  v_clob       CLOB := EMPTY_CLOB();
  v_row_json   CLOB;
  v_first_row  BOOLEAN := TRUE;
BEGIN
  v_cursor := DBMS_SQL.OPEN_CURSOR;
  DBMS_SQL.PARSE(v_cursor, p_query, DBMS_SQL.NATIVE);
  DBMS_SQL.DESCRIBE_COLUMNS(v_cursor, v_colcnt, v_desc_tab);
  
  FOR i IN 1 .. v_colcnt LOOP
    DBMS_SQL.DEFINE_COLUMN(v_cursor, i, v_varchar2, 4000);
  END LOOP;
  
  v_status := DBMS_SQL.EXECUTE(v_cursor);
  DBMS_LOB.CREATETEMPORARY(v_clob, TRUE);
  DBMS_LOB.WRITEAPPEND(v_clob, LENGTH('['), '[');
  
  LOOP
    IF DBMS_SQL.FETCH_ROWS(v_cursor) = 0 THEN
      EXIT;
    END IF;
    
    v_row_json := '{';
    FOR i IN 1 .. v_colcnt LOOP
      DBMS_SQL.COLUMN_VALUE(v_cursor, i, v_varchar2);
      v_varchar2 := REPLACE(REPLACE(NVL(v_varchar2,''), '\', '\\'), '"', '\"');
      
      DECLARE
        v_tmp NUMBER;
        is_num BOOLEAN := FALSE;
      BEGIN
        BEGIN
          v_tmp := TO_NUMBER(v_varchar2);
          is_num := TRUE;
        EXCEPTION
          WHEN OTHERS THEN
            is_num := FALSE;
        END;
        
        IF is_num THEN
          v_row_json := v_row_json || '"' || v_desc_tab(i).col_name || '":' || v_varchar2;
        ELSE
          v_row_json := v_row_json || '"' || v_desc_tab(i).col_name || '":"' || v_varchar2 || '"';
        END IF;
      END;
      
      IF i < v_colcnt THEN
        v_row_json := v_row_json || ',';
      END IF;
    END LOOP;
    
    v_row_json := v_row_json || '}';
    
    IF v_first_row THEN
      v_first_row := FALSE;
      DBMS_LOB.WRITEAPPEND(v_clob, LENGTH(v_row_json), v_row_json);
    ELSE
      DBMS_LOB.WRITEAPPEND(v_clob, LENGTH(','||v_row_json), ','||v_row_json);
    END IF;
  END LOOP;
  
  DBMS_LOB.WRITEAPPEND(v_clob, LENGTH(']'), ']');
  DBMS_SQL.CLOSE_CURSOR(v_cursor);
  RETURN v_clob;
  
EXCEPTION
  WHEN NO_DATA_FOUND THEN
    IF DBMS_SQL.IS_OPEN(v_cursor) THEN DBMS_SQL.CLOSE_CURSOR(v_cursor); END IF;
    RETURN '[ ]';
  WHEN VALUE_ERROR THEN
    IF DBMS_SQL.IS_OPEN(v_cursor) THEN DBMS_SQL.CLOSE_CURSOR(v_cursor); END IF;
    RETURN '{"error":"value_error converting column"}';
  WHEN OTHERS THEN
    IF DBMS_SQL.IS_OPEN(v_cursor) THEN DBMS_SQL.CLOSE_CURSOR(v_cursor); END IF;
    RETURN '{"error":"unexpected: ' || SQLERRM || '"}';
END criaJson;
/

CREATE OR REPLACE FUNCTION fnc_valsenha(p_senha IN VARCHAR2) RETURN VARCHAR2 IS
  v_len NUMBER;
  has_digit BOOLEAN := FALSE;
  has_upper BOOLEAN := FALSE;
  e_no_digit EXCEPTION;
  e_no_upper EXCEPTION;
BEGIN
  IF p_senha IS NULL THEN
    RETURN 'ERRO: Senha nula';
  END IF;
  
  v_len := LENGTH(p_senha);
  
  IF v_len < 6 THEN
    RETURN 'ERRO: Senha muito curta (min 6)';
  END IF;
  
  FOR i IN 1 .. v_len LOOP
    DECLARE
      ch CHAR(1) := SUBSTR(p_senha, i, 1);
    BEGIN
      IF ch BETWEEN '0' AND '9' THEN
        has_digit := TRUE;
      END IF;
      IF ch BETWEEN 'A' AND 'Z' THEN
        has_upper := TRUE;
      END IF;
    EXCEPTION
      WHEN VALUE_ERROR THEN
        NULL;
    END;
  END LOOP;
  
  IF NOT has_digit THEN
    RAISE e_no_digit;
  END IF;
  
  IF NOT has_upper THEN
    RAISE e_no_upper;
  END IF;
  
  RETURN 'OK';
  
EXCEPTION
  WHEN VALUE_ERROR THEN
    RETURN 'ERRO: Value error na validação';
  WHEN NO_DATA_FOUND THEN
    RETURN 'ERRO: Dado não encontrado (improvável aqui)';
  WHEN e_no_digit THEN
    RETURN 'ERRO: Deve conter ao menos um dígito';
  WHEN e_no_upper THEN
    RETURN 'ERRO: Deve conter ao menos uma letra maiúscula';
  WHEN OTHERS THEN
    RETURN 'ERRO INESPERADO: ' || SQLERRM;
END fnc_valsenha;
/

-- ============================================================
-- PARTE 6: Procedures
-- ============================================================

CREATE OR REPLACE PROCEDURE proc_json IS
  v_sql   VARCHAR2(4000);
  v_json  CLOB;
BEGIN
  v_sql := 'SELECT m.id_moto, m.placa_moto, m.modelo_moto, m.quilometragem_moto, c.id_contrato, c.valor_toral_contrato, p.id_patio, p.nome_patio ' ||
           'FROM mt_moto_java m ' ||
           'JOIN mt_contrato_java c ON m.contrato_moto_id = c.id_contrato ' ||
           'JOIN mt_patio_java p ON m.moto_patio_atual_id = p.id_patio ' ||
           'ORDER BY p.id_patio, c.id_contrato, m.id_moto';
  
  v_json := criaJson(v_sql);
  DBMS_OUTPUT.PUT_LINE('JSON DAS TABELAS');
  DBMS_OUTPUT.PUT_LINE(v_json);
  
EXCEPTION
  WHEN NO_DATA_FOUND THEN
    DBMS_OUTPUT.PUT_LINE('Proc1: Nenhum dado encontrado para o JOIN.');
  WHEN VALUE_ERROR THEN
    DBMS_OUTPUT.PUT_LINE('Proc1: Erro de conversão de valor (VALUE_ERROR).');
  WHEN OTHERS THEN
    DBMS_OUTPUT.PUT_LINE('Proc1: Erro inesperado: ' || SQLERRM);
END proc_json;
/

CREATE OR REPLACE PROCEDURE proc_motos_p_patio IS
  CURSOR cur_agencias IS
    SELECT DISTINCT moto_patio_atual_id AS idpatio
    FROM mt_moto_java
    WHERE moto_patio_atual_id IS NOT NULL
    ORDER BY moto_patio_atual_id;
    
  CURSOR cur_contas(p_idpatio NUMBER) IS
    SELECT DISTINCT contrato_moto_id AS idcon
    FROM mt_moto_java
    WHERE moto_patio_atual_id = p_idpatio
      AND contrato_moto_id IS NOT NULL
    ORDER BY contrato_moto_id;
    
  v_idpatio    NUMBER;
  v_idcon      NUMBER;
  v_sum_line   NUMBER := 0;
  v_subtotal   NUMBER := 0;
  v_total      NUMBER := 0;
  v_val        NUMBER;
BEGIN
  DBMS_OUTPUT.PUT_LINE('AGENCIA | CONTA | SOMA(quilometragem)');
  DBMS_OUTPUT.PUT_LINE('---------------------------------');
  
  FOR rec_agencia IN cur_agencias LOOP
    v_idpatio := rec_agencia.idpatio;
    v_subtotal := 0;
    
    FOR rec_con IN cur_contas(v_idpatio) LOOP
      v_idcon := rec_con.idcon;
      v_sum_line := 0;
      
      FOR r IN (SELECT quilometragem_moto val FROM mt_moto_java
                WHERE moto_patio_atual_id = v_idpatio
                  AND contrato_moto_id = v_idcon) LOOP
        BEGIN
          v_val := NVL(r.val, 0);
          v_sum_line := v_sum_line + v_val;
        EXCEPTION
          WHEN VALUE_ERROR THEN
            DBMS_OUTPUT.PUT_LINE('Valor inválido para quilometragem em agencia '||v_idpatio||' conta '||v_idcon);
        END;
      END LOOP;
      
      DBMS_OUTPUT.PUT_LINE(NVL(TO_CHAR(v_idpatio),'NULL') || ' | ' || NVL(TO_CHAR(v_idcon),'NULL') || ' | ' || TO_CHAR(v_sum_line));
      v_subtotal := v_subtotal + v_sum_line;
    END LOOP;
    
    DBMS_OUTPUT.PUT_LINE(TO_CHAR(v_idpatio) || ' | ' || 'NULL' || ' | ' || TO_CHAR(v_subtotal));
    v_total := v_total + v_subtotal;
  END LOOP;
  
  DBMS_OUTPUT.PUT_LINE('NULL | NULL | ' || TO_CHAR(v_total));
  
EXCEPTION
  WHEN NO_DATA_FOUND THEN
    DBMS_OUTPUT.PUT_LINE('Proc2: Nenhum dado encontrado na tabela fato.');
  WHEN VALUE_ERROR THEN
    DBMS_OUTPUT.PUT_LINE('Proc2: Erro de conversão numérica durante os somatórios.');
  WHEN OTHERS THEN
    DBMS_OUTPUT.PUT_LINE('Proc2: Erro inesperado: ' || SQLERRM);
END proc_motos_p_patio;
/

-- ============================================================
-- PARTE 7: Packages
-- ============================================================

CREATE OR REPLACE PACKAGE pkg_json_util AS 
    FUNCTION criaJson(p_query IN VARCHAR2) RETURN CLOB; 
END pkg_json_util;
/

CREATE OR REPLACE PACKAGE BODY pkg_json_util AS
  FUNCTION criaJson(p_query IN VARCHAR2) RETURN CLOB IS
    v_cursor SYS_REFCURSOR;
    v_json   CLOB := '[';
    v_first  BOOLEAN := TRUE;
    v_idmoto           NUMBER;
    v_placamoto        VARCHAR2(100);
    v_modelomoto       VARCHAR2(100);
    v_quilometragemmoto NUMBER;
  BEGIN
    OPEN v_cursor FOR p_query;
    LOOP
      FETCH v_cursor INTO v_idmoto, v_placamoto, v_modelomoto, v_quilometragemmoto;
      EXIT WHEN v_cursor%NOTFOUND;
      
      IF NOT v_first THEN
        v_json := v_json || ',';
      END IF;
      
      v_json := v_json || '{"idmoto":' || v_idmoto ||
                ',"placamoto":"' || v_placamoto ||
                '","modelomoto":"' || v_modelomoto ||
                '","quilometragemmoto":' || v_quilometragemmoto || '}';
      v_first := FALSE;
    END LOOP;
    
    CLOSE v_cursor;
    v_json := v_json || ']';
    RETURN v_json;
  END criaJson;
END pkg_json_util;
/

CREATE OR REPLACE PACKAGE pkg_seguranca AS
  FUNCTION fnc_valsenha(p_senha IN VARCHAR2) RETURN VARCHAR2;
END pkg_seguranca;
/

CREATE OR REPLACE PACKAGE BODY pkg_seguranca AS
  FUNCTION fnc_valsenha(p_senha IN VARCHAR2) RETURN VARCHAR2 IS
    v_result VARCHAR2(200);
  BEGIN
    IF LENGTH(p_senha) < 8 THEN
      v_result := 'SENHA CURTA';
    ELSIF REGEXP_LIKE(p_senha, '^[A-Za-z0-9]+$') THEN
      v_result := 'SENHA OK';
    ELSE
      v_result := 'SENHA INVÁLIDA';
    END IF;
    RETURN v_result;
  END fnc_valsenha;
END pkg_seguranca;
/

CREATE OR REPLACE PACKAGE pkg_relatorios AS
  PROCEDURE proc_json;
  PROCEDURE proc_motos_p_patio;
END pkg_relatorios;
/

CREATE OR REPLACE PACKAGE BODY pkg_relatorios AS
  PROCEDURE proc_json IS
    v_json CLOB;
  BEGIN
    v_json := pkg_json_util.criaJson('SELECT id_moto, placa_moto, modelo_moto, quilometragem_moto FROM mt_moto_java');
    DBMS_OUTPUT.PUT_LINE(v_json);
  END proc_json;
  
  PROCEDURE proc_motos_p_patio IS
    v_total_motos NUMBER;
  BEGIN
    SELECT COUNT(*) INTO v_total_motos FROM mt_moto_java;
    DBMS_OUTPUT.PUT_LINE('Total de motos: ' || v_total_motos);
  END proc_motos_p_patio;
END pkg_relatorios;
/

CREATE OR REPLACE PACKAGE pkg_auditoria AS
  PROCEDURE grava_auditoria(
    p_tabela    IN VARCHAR2,
    p_usuario   IN VARCHAR2,
    p_operacao  IN VARCHAR2,
    p_old       IN CLOB,
    p_new       IN CLOB
  );
END pkg_auditoria;
/

CREATE OR REPLACE PACKAGE BODY pkg_auditoria AS
  PROCEDURE grava_auditoria(
    p_tabela    IN VARCHAR2,
    p_usuario   IN VARCHAR2,
    p_operacao  IN VARCHAR2,
    p_old       IN CLOB,
    p_new       IN CLOB
  ) IS
  BEGIN
    INSERT INTO mt_auditoria (tabela_nome, usuario_nome, tipo_operacao, data_hora, valores_old, valores_new)
    VALUES (p_tabela, p_usuario, p_operacao, SYSDATE, p_old, p_new);
  EXCEPTION
    WHEN OTHERS THEN
      DBMS_OUTPUT.PUT_LINE('Erro ao gravar auditoria: ' || SQLERRM);
  END grava_auditoria;
END pkg_auditoria;
/

-- ============================================================
-- PARTE 8: Triggers de Auditoria
-- ============================================================

CREATE OR REPLACE TRIGGER trg_audit_mt_moto
AFTER INSERT OR UPDATE OR DELETE ON mt_moto_java
FOR EACH ROW
DECLARE
  v_user VARCHAR2(100) := NVL(USER, 'UNKNOWN');
  v_old  CLOB := NULL;
  v_new  CLOB := NULL;
  v_err  VARCHAR2(4000);
  
  PROCEDURE row_to_clob(p_prefix VARCHAR2, p_idmoto NUMBER, p_placa VARCHAR2, p_modelo VARCHAR2, p_qt NUMBER, p_est VARCHAR2) IS
  BEGIN
    IF p_prefix = 'OLD' THEN
      v_old := '{ "idmoto":' || NVL(TO_CHAR(p_idmoto),'null') || ', "placa":"' || NVL(p_placa,'') || '", "modelo":"' || NVL(p_modelo,'') || '", "quilometragem":' || NVL(TO_CHAR(p_qt),'null') || ', "estado":"' || NVL(p_est,'') || '" }';
    ELSE
      v_new := '{ "idmoto":' || NVL(TO_CHAR(p_idmoto),'null') || ', "placa":"' || NVL(p_placa,'') || '", "modelo":"' || NVL(p_modelo,'') || '", "quilometragem":' || NVL(TO_CHAR(p_qt),'null') || ', "estado":"' || NVL(p_est,'') || '" }';
    END IF;
  END;
BEGIN
  IF INSERTING THEN
    row_to_clob('NEW', :NEW.id_moto, :NEW.placa_moto, :NEW.modelo_moto, :NEW.quilometragem_moto, NVL(TO_CHAR(:NEW.estado_moto),''));
    INSERT INTO mt_auditoria(tabela_nome, usuario_nome, tipo_operacao, data_hora, valores_old, valores_new)
    VALUES('MT_MOTO_JAVA', v_user, 'INSERT', SYSDATE, NULL, v_new);
  ELSIF UPDATING THEN
    row_to_clob('OLD', :OLD.id_moto, :OLD.placa_moto, :OLD.modelo_moto, :OLD.quilometragem_moto, NVL(TO_CHAR(:OLD.estado_moto),''));
    row_to_clob('NEW', :NEW.id_moto, :NEW.placa_moto, :NEW.modelo_moto, :NEW.quilometragem_moto, NVL(TO_CHAR(:NEW.estado_moto),''));
    INSERT INTO mt_auditoria(tabela_nome, usuario_nome, tipo_operacao, data_hora, valores_old, valores_new)
    VALUES('MT_MOTO_JAVA', v_user, 'UPDATE', SYSDATE, v_old, v_new);
  ELSIF DELETING THEN
    row_to_clob('OLD', :OLD.id_moto, :OLD.placa_moto, :OLD.modelo_moto, :OLD.quilometragem_moto, NVL(TO_CHAR(:OLD.estado_moto),''));
    INSERT INTO mt_auditoria(tabela_nome, usuario_nome, tipo_operacao, data_hora, valores_old, valores_new)
    VALUES('MT_MOTO_JAVA', v_user, 'DELETE', SYSDATE, v_old, NULL);
  END IF;
  
EXCEPTION
  WHEN VALUE_ERROR THEN
    INSERT INTO mt_auditoria(tabela_nome, usuario_nome, tipo_operacao, data_hora, valores_old, valores_new)
    VALUES('MT_MOTO_JAVA', v_user, 'AUDIT_ERROR', SYSDATE, 'VALUE_ERROR', NULL);
  WHEN OTHERS THEN
    v_err := SQLERRM;
    INSERT INTO mt_auditoria(tabela_nome, usuario_nome, tipo_operacao, data_hora, valores_old, valores_new)
    VALUES('MT_MOTO_JAVA', v_user, 'AUDIT_ERROR', SYSDATE, v_err, NULL);
END;
/

CREATE OR REPLACE TRIGGER trg_mt_moto_audit
AFTER INSERT OR UPDATE OR DELETE ON mt_moto_java
FOR EACH ROW
DECLARE
  v_usuario VARCHAR2(50);
  v_old CLOB;
  v_new CLOB;
BEGIN
  v_usuario := NVL(USER, 'SISTEMA');
  
  IF INSERTING THEN
    v_new := '{"idmoto":' || :NEW.id_moto || ',"placa":"' || :NEW.placa_moto || '"}';
    pkg_auditoria.grava_auditoria('MT_MOTO_JAVA', v_usuario, 'INSERT', NULL, v_new);
  ELSIF UPDATING THEN
    v_old := '{"idmoto":' || :OLD.id_moto || ',"placa":"' || :OLD.placa_moto || '"}';
    v_new := '{"idmoto":' || :NEW.id_moto || ',"placa":"' || :NEW.placa_moto || '"}';
    pkg_auditoria.grava_auditoria('MT_MOTO_JAVA', v_usuario, 'UPDATE', v_old, v_new);
  ELSIF DELETING THEN
    v_old := '{"idmoto":' || :OLD.id_moto || ',"placa":"' || :OLD.placa_moto || '"}';
    pkg_auditoria.grava_auditoria('MT_MOTO_JAVA', v_usuario, 'DELETE', v_old, NULL);
  END IF;
END;
/

